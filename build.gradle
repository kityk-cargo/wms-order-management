plugins {
	id 'java'
	id 'groovy'
	id 'org.springframework.boot' version '3.5.0-SNAPSHOT'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'checkstyle'
	id 'pmd'
	id 'com.github.spotbugs' version '6.1.6'
	id 'org.springdoc.openapi-gradle-plugin' version '1.8.0'
}

group = 'cargo.kityk.wms'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven { url = uri("https://repo.spring.io/milestone") }
	maven { url = uri("https://repo.spring.io/snapshot") }
}

ext {
	set('springCloudVersion', "2025.0.0-M1")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation('org.springframework.boot:spring-boot-starter-data-jpa')
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	
	// Explicit Liquibase dependency
	implementation 'org.liquibase:liquibase-core'
	
	implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
	implementation 'org.springframework.kafka:spring-kafka'
	implementation 'org.apache.groovy:groovy-all:4.0.25'
	
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	
	runtimeOnly 'org.postgresql:postgresql'
	
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'
	implementation 'org.springdoc:springdoc-openapi-starter-common:2.3.0'
	implementation 'org.apache.groovy:groovy-json:4.0.25'
	
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.kafka:spring-kafka-test'
	testImplementation 'com.h2database:h2'
	testImplementation 'org.testcontainers:testcontainers:1.20.5'
	testImplementation 'org.testcontainers:junit-jupiter:1.20.5'
	testImplementation 'org.testcontainers:postgresql:1.20.5'
	
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

// Add classpath inclusion for testing across all operating systems
tasks.withType(Test) {
	def liquibaseTestPath = System.getenv("LIQUIBASE_TEST_CLASSPATH") ?: "$projectDir/../wms-main/liquibase"
	classpath += files(liquibaseTestPath)
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ['-parameters']
}

tasks.withType(GroovyCompile) {
    options.compilerArgs += ['-parameters']
}

compileJava.dependsOn = compileJava.dependsOn.findAll { 
    it != compileGroovy 
}
compileGroovy.dependsOn compileJava
compileGroovy.classpath += files(compileJava.destinationDirectory)

checkstyle {
	toolVersion = '10.12.4'
	configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
}

pmd {
	toolVersion = '6.55.0'
	ruleSets = ["category/java/bestpractices.xml", "category/java/errorprone.xml"]
}

spotbugs {
	toolVersion = '4.8.3'
	excludeFilter = file("${rootDir}/config/spotbugs/exclude.xml")
	ignoreFailures = true
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
	reports {
		xml.required = false
		html.required = true
	}
}


springBoot {
    mainClass = 'cargo.kityk.wms.order.application.OrderApplication'
}

openApi {
	outputDir = file("$buildDir/api-docs")
	outputFileName = 'openapi.json'
	waitTimeInSeconds = 120
	apiDocsUrl = "http://localhost:8080/docs"
	customBootRun {
		mainClass = 'cargo.kityk.wms.order.openapi.OpenApiGeneratorApplication'
		args = ['--spring.profiles.active=test,openapi']
	}
}
